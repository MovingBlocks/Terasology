// Copyright 2020 The Terasology Foundation
// SPDX-License-Identifier: Apache-2.0

// This magically allows subdirs to become included builds
// https://docs.gradle.org/6.4.1/userguide/composite_builds.html
file(".").eachDir { possibleIncludedBuildDirectory ->
    File buildFile = new File(possibleIncludedBuildDirectory, "build.gradle")
    File settingsFile = new File(possibleIncludedBuildDirectory, "settings.gradle")

    if (buildFile.exists() && settingsFile.exists()) {
        logger.info("{} will be included in the composite build.",
            rootDir.relativePath(possibleIncludedBuildDirectory))
        includeBuild(possibleIncludedBuildDirectory)
    } else {
        logger.warn("{} REJECTED as an included build. build.gradle: {}, settings.gradle: {}",
                rootDir.relativePath(possibleIncludedBuildDirectory),
                buildFile.exists() ? "present" : "MISSING",
                settingsFile.exists() ? "present" : "MISSING"
        )
    }
}
